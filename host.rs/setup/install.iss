; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Memberry host application"
#define MyAppVersion "0.1.0"
#define MyAppPublisher "Denis Shemanaev"
#define MyAppURL "https://github.com/shemanaev/memberry"
#define AppVersion StringChange(MyAppVersion, ".0", "")
#ifndef ChromeExtensionId
  #define ChromeExtensionId "%REPLACE_ME%"
#endif

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{0CB3690E-F418-4F22-87EE-A22E28C10A3A}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={userpf}\Memberry
DisableProgramGroupPage=yes
OutputDir=..\dist
OutputBaseFilename=memberry-install-v{#AppVersion}
Compression=lzma
SolidCompression=yes
PrivilegesRequired=lowest
LicenseFile=..\LICENSE
ShowLanguageDialog=auto
ArchitecturesInstallIn64BitMode=x64
MinVersion=0,6.1

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "armenian"; MessagesFile: "compiler:Languages\Armenian.islu"
Name: "brazilianportuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"
Name: "catalan"; MessagesFile: "compiler:Languages\Catalan.isl"
Name: "corsican"; MessagesFile: "compiler:Languages\Corsican.isl"
Name: "czech"; MessagesFile: "compiler:Languages\Czech.isl"
Name: "danish"; MessagesFile: "compiler:Languages\Danish.isl"
Name: "dutch"; MessagesFile: "compiler:Languages\Dutch.isl"
Name: "finnish"; MessagesFile: "compiler:Languages\Finnish.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "greek"; MessagesFile: "compiler:Languages\Greek.isl"
Name: "hebrew"; MessagesFile: "compiler:Languages\Hebrew.isl"
Name: "hungarian"; MessagesFile: "compiler:Languages\Hungarian.isl"
Name: "icelandic"; MessagesFile: "compiler:Languages\Icelandic.isl"
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "japanese"; MessagesFile: "compiler:Languages\Japanese.isl"
Name: "nepali"; MessagesFile: "compiler:Languages\Nepali.islu"
Name: "norwegian"; MessagesFile: "compiler:Languages\Norwegian.isl"
Name: "polish"; MessagesFile: "compiler:Languages\Polish.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "scottishgaelic"; MessagesFile: "compiler:Languages\ScottishGaelic.isl"
Name: "serbiancyrillic"; MessagesFile: "compiler:Languages\SerbianCyrillic.isl"
Name: "serbianlatin"; MessagesFile: "compiler:Languages\SerbianLatin.isl"
Name: "slovenian"; MessagesFile: "compiler:Languages\Slovenian.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "turkish"; MessagesFile: "compiler:Languages\Turkish.isl"
Name: "ukrainian"; MessagesFile: "compiler:Languages\Ukrainian.isl"

[Files]
Source: "..\manifests\chrome.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\manifests\firefox.json"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\target\release\memberry_host.exe"; DestDir: "{app}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Registry]
Root: HKCU; Subkey: "Software\Google\Chrome\NativeMessagingHosts\com.shemanaev.memberry"; Flags: uninsdeletekey
Root: HKCU; Subkey: "Software\Google\Chrome\NativeMessagingHosts\com.shemanaev.memberry"; ValueType: string; ValueData: "{app}\chrome.json"; Flags: uninsdeletevalue
Root: HKCU; Subkey: "Software\Mozilla\NativeMessagingHosts\com.shemanaev.memberry"; Flags: uninsdeletekey
Root: HKCU; Subkey: "Software\Mozilla\NativeMessagingHosts\com.shemanaev.memberry"; ValueType: string; ValueData: "{app}\firefox.json"; Flags: uninsdeletevalue

[Code]
// https://stackoverflow.com/a/20181221
function FileReplaceString(const FileName, SearchString, ReplaceString: string): boolean;
var
  MyFile: TStrings;
  MyText: string;
begin
  MyFile := TStringList.Create;

  try
    result := true;
    try
      MyFile.LoadFromFile(FileName);
      MyText := MyFile.Text;

      { Only save if text has been changed. }
      if StringChangeEx(MyText, SearchString, ReplaceString, True) > 0 then
      begin;
        MyFile.Text := MyText;
        MyFile.SaveToFile(FileName);
      end;
    except
      result := false;
    end;
  finally
    MyFile.Free;
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
  ExePath: string;
  ExtId: string;
begin
  if CurStep = ssDone then
  begin
    ExtId := '{#SetupSetting("ChromeExtensionId")}';
    ExePath := ExpandConstant('{app}\memberry_host.exe');
    StringChangeEx(ExePath, '\', '\\', True);
    FileReplaceString(ExpandConstant('{app}\chrome.json'), '%EXEPATH%', ExePath);
    FileReplaceString(ExpandConstant('{app}\chrome.json'), '%EXTENSION%', ExtId);
    FileReplaceString(ExpandConstant('{app}\firefox.json'), '%EXEPATH%', ExePath);
  end;
end;
